plugins {
	id 'fabric-loom' version '0.10.+'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com" }
	maven {
		name = "Enchantment Descriptions"
		url = 'https://maven.blamejared.com'
	}
	maven {
		name = "Patchouli" // https://github.com/Vazkii/Patchouli
		url = 'https://maven.blamejared.com'
	}
	maven { url 'https://maven.wispforest.io/' } // owo lib
	//maven { url "https://jitpack.io" } // incubus core
}

dependencies {
	// fabric
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// dependencies
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"){
		exclude(group: "net.fabricmc.fabric-api")
	}
	modApi("com.terraformersmc:modmenu:${project.modmenu_version}")

	modImplementation "vazkii.patchouli:Patchouli:${project.patchouli_version}"	// https://maven.blamejared.com/vazkii/patchouli/Patchouli/
	modImplementation "io.wispforest:owo-lib:${project.owo_version}+1.18"

	modCompileOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
	modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"

	// breaks outside of dev env currently.
	//modImplementation include("com.github.devs-immortal:Incubus-Core:${project.incubus_core_version}")

	// Soft dependencies:
	// https://maven.blamejared.com/net/darkhax/enchdesc/
	// modImplementation("net.darkhax.enchdesc:EnchantmentDescriptions-Fabric-${project.enchantment_descriptions_mc_version}:${project.enchantment_descriptions_mod_version}")
	// https://github.com/Gegy/colored-lights // uses colored_lights/light_colors.json
}

loom {
	accessWidenerPath = file("src/main/resources/spectrum.accesswidener")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	it.options.release = 17 // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}